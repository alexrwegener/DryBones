buildscript {
  repositories {
    jcenter()
  }
  dependencies {
    classpath 'com.neenbedankt.gradle.plugins:android-apt:1.2'
    classpath 'net.ltgt.gradle:gradle-errorprone-plugin:latest.release'
  }
}

repositories {
  maven { url 'https://oss.sonatype.org/content/repositories/snapshots/' } //TODO Remove once flow and mortar are released
}

// Manifest version information!
def versionMajor = 1
def versionMinor = 0
def versionPatch = 0
def versionBuild = 1
// bump for dogfood builds, public betas, etc.

// Constants
def buildTime = new Date().format("yyyy-MM-dd'T'HH:mm'Z'", TimeZone.getTimeZone("UTC"))
def gitSha = 'git rev-parse --short HEAD'.execute([], project.rootDir).text.trim()

apply plugin: 'com.android.application'
apply plugin: 'idea'
apply plugin: 'android-apt'
apply plugin: 'errorprone'

android {
  compileSdkVersion 21
  buildToolsVersion "21.1.2"

  defaultConfig {
    applicationId "com.github.ihal20.drybones"
    versionCode versionMajor * 10000 + versionMinor * 1000 + versionPatch * 100 + versionBuild
    versionName "${versionMajor}.${versionMinor}.${versionPatch}"
    minSdkVersion 15
    targetSdkVersion 21
    buildConfigField "String", "BUILD_TIME", "\"${buildTime}\""
    buildConfigField "String", "GIT_SHA", "\"${gitSha}\""
  }
  signingConfigs {
    debug {
      storeFile file('keystores/debug.keystore')
    }
    release {
      //TODO create release keystore
    }
  }
  buildTypes {
    debug {
      versionNameSuffix "-SNAPSHOT"
      applicationIdSuffix '.debug'
      signingConfig signingConfigs.debug
    }
    release {
      minifyEnabled false
      //TODO Add proguard after dagger 2.0 comes out
      //proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
    }
  }
  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_7
    targetCompatibility JavaVersion.VERSION_1_7
  }
}

dependencies {
  //Android
  //TODO What material design components do we want to bring in?
  //compile 'com.android.support:cardview-v7:21.0.0'
  //compile 'com.android.support:recyclerview-v7:21.0.0'
  compile 'com.android.support:appcompat-v7:21.0.3'
  compile 'com.android.support:support-v4:21.0.3'

  //RxJava
  compile 'io.reactivex:rxjava:1.0.4'
  compile 'io.reactivex:rxandroid:0.24.0'

  //Auto Parcel
  apt 'com.github.frankiesardo:auto-parcel-processor:0.2'
  compile 'com.github.frankiesardo:auto-parcel:0.2'

  //Square
  apt 'com.squareup.dagger:dagger-compiler:1.2.2'
  compile 'com.squareup.dagger:dagger:1.2.2'
  apt 'com.jakewharton:butterknife:6.0.0'
  compile 'com.jakewharton:butterknife:6.0.0'
  compile 'com.squareup.okhttp:okhttp:2.2.0'
  compile 'com.squareup.okhttp:okhttp-urlconnection:2.2.0'
  compile 'com.squareup.retrofit:retrofit:1.9.0'
  compile 'com.squareup.picasso:picasso:2.4.0'
  compile 'com.jakewharton.timber:timber:2.5.1'
  compile 'com.squareup.flow:flow:0.9-SNAPSHOT'
  compile 'com.squareup.mortar:mortar:0.17-SNAPSHOT'
  compile 'com.squareup.mortar:dagger1support:0.17-SNAPSHOT'

  //Utils
  compile 'joda-time:joda-time:2.7'
  compile 'com.google.code.gson:gson:2.3.1'

  //Debug
  //Square debug
  debugCompile 'com.squareup.retrofit:retrofit-mock:1.9.0'
  debugCompile 'com.jakewharton.scalpel:scalpel:1.1.2'
  debugCompile 'com.jakewharton.madge:madge:1.1.1'
}

idea {
  module {
    downloadJavadoc = true
    downloadSources = true
  }
}